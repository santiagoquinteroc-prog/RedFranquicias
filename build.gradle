plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'com.red'
version = '0.0.1-SNAPSHOT'
description = 'API de Franquicias con Spring WebFlux'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.6.0'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	runtimeOnly 'io.asyncer:r2dbc-mysql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation platform('org.testcontainers:testcontainers-bom:1.19.3')
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mysql'
	testImplementation 'org.mockito:mockito-core'
	testImplementation 'org.mockito:mockito-junit-jupiter'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}
jacoco {
	toolVersion = "0.8.11"
}

def jacocoExcludes = [
		'**/config/**',
		'**/infrastructure/config/**',
		'**/infrastructure/entrypoint/web/mapper/**',
		'**/exception/**',
		'**/*Application*'
]

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy tasks.named('jacocoTestReport')
}

tasks.named('jacocoTestReport', JacocoReport) {
	dependsOn tasks.named('test')

	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}

	classDirectories.setFrom(
			sourceSets.main.output.classesDirs.asFileTree.matching {
				exclude jacocoExcludes
			}
	)
}

tasks.named('jacocoTestCoverageVerification', JacocoCoverageVerification) {
	dependsOn tasks.named('test')

	classDirectories.setFrom(
			sourceSets.main.output.classesDirs.asFileTree.matching {
				exclude jacocoExcludes
			}
	)

	violationRules {
		rule {
			limit {
				minimum = 0.80
			}
		}
	}
}

tasks.named('check') {
	dependsOn tasks.named('jacocoTestCoverageVerification')
}
